/*
 * Copyright Amazon.com, Inc. or its affiliates. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

apply plugin: 'maven-publish'
apply plugin: 'signing'

// No docs will be published for CRT since it's only used internally to the SDK.
tasks.register("javadocJar", Jar) {
    archiveClassifier.set("javadoc")
    from()
}

publishing {
    repositories {
        maven { name = "testLocal"; url = "$rootProject.buildDir/m2" }
        maven {
            name = "awsCodeArtifact"
            url = project.findProperty("codeartifact.url")
            credentials {
                username = "aws"
                password = project.findProperty("codeartifact.token") ?: System.getenv("CODEARTIFACT_TOKEN")
            }
        }
    }

    publications.all {
        project.afterEvaluate {
            pom {
                name = project.name
                description = project.description
                url = "https://github.com/awslabs/aws-crt-kotlin"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "aws-crt-kotlin"
                        name = "AWS SDK Kotlin Team"
                        // TODO - team email?
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/awslabs/aws-crt-kotlin.git"
                    developerConnection = "scm:git:ssh://github.com/awslabs/aws-crt-kotlin.git"
                    url = "https://github.com/awslabs/aws-crt-kotlin"
                }

                artifact(tasks["javadocJar"])
            }
        }
    }

    if (project.hasProperty("signingKey") && project.hasProperty("signingPassword")) {
        signing {
            useInMemoryPgpKeys(
                    (String) project.property("signingKey"),
                    (String) project.property("signingPassword")
            )
            sign(publications)
        }
    }
}

tasks.register('publishToAwsCodeArtifact') {
    dependsOn 'publishAllPublicationsToAwsCodeArtifactRepository'
    group 'publishing'
}
